plugins {
    id 'java'
    id 'jacoco'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

java {
    withJavadocJar()
    withSourcesJar()
}

compileJava {
    sourceCompatibility = '8'
    targetCompatibility = '8'
}



repositories {
    mavenCentral()
    mavenLocal()
}


dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    implementation group: 'io.github.ryucc', name: 'ReRe', version: '0.0.8'
    implementation group: 'software.amazon.awssdk', name: 'dynamodb', version: '2.31.10'
}

test {
    useJUnitPlatform()
}


javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

group = 'io.github.ryucc'
version = '0.0.8'

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'ReRe-aws-components'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'ReRe-aws-components-' + version
                description = 'A concise description of my library'
                url = 'https://github.com/ryucc/ReRe-aws-components'
                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://mit-license.org'
                    }
                }
                developers {
                    developer {
                        id = 'ryucc'
                        name = 'Kurtis Liu'
                        email = 'shortyliu@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/ryucc/ReRe-grpc-components.git'
                    developerConnection = 'scm:git:ssh://github.com:ryucc/ReRe-grpc-components.git'
                    url = 'https://github.com/ryucc/ReRe-grpc-components'
                }
            }
        }
    }
    repositories {
        maven {
            // change URLs to point to your repos, e.g. http://my.org/repo
            def releasesRepoUrl = layout.buildDirectory.dir('repos/releases')
            def snapshotsRepoUrl = layout.buildDirectory.dir('repos/snapshots')
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}
